buildscript {
    repositories {
        flatDir dirs: "${System.getenv("PROGUARD_HOME")}/lib"
    }
    dependencies {
        classpath ':proguard:'
    }
}

jar {
    exclude 'META-INF/LICENSE/'
    exclude 'META-INF/NOTICE/'
    exclude 'META-INF/maven/'
    exclude 'META-INF/native-image/'
    exclude 'META-INF/INDEX.LIST'
    exclude 'META-INF/io.netty.versions.properties'
    exclude 'OSGI-OPT/'
    exclude 'LICENSE'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': project.MainClass
        attributes 'Version-Code': String.valueOf(GitVC.VERSION_CODE)
        attributes 'Version-Name': String.valueOf(GitVC.VERSION_NAME)
        attributes 'Last-Commit-SHA': String.valueOf(GitVC.LAST_COMMIT_SHA)
        attributes 'Last-Commit-Date': String.valueOf(GitVC.LAST_COMMIT_DATE)
        attributes 'Build-Date': String.valueOf(new Date())
    }
}

task proguardJar(
        group: "build",
        type: proguard.gradle.ProGuardTask,
        dependsOn: jar
) {
    configuration project.file("proguard-rules.txt")
    injars "${project.buildDir}/libs/${project.name}-${project.version}.jar"
    outjars rootProject.file("dists/${project.version}/${project.OutJarName ?: (project.name + ".jar")}")
}