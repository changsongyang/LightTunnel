apply plugin: "org.jetbrains.kotlin.jvm"

group = "${rootProject.group}.core"
version = rootProject.version

sourceCompatibility = rootProject.jvm_version

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    // Netty
    def netty_version = "4.1.56.Final"
    compile "io.netty:netty-buffer:$netty_version"
    compile "io.netty:netty-codec:$netty_version"
    compile "io.netty:netty-codec-http:$netty_version"
    compile "io.netty:netty-common:$netty_version"
    compile "io.netty:netty-handler:$netty_version"
    compile "io.netty:netty-resolver:$netty_version"
    compile "io.netty:netty-transport:$netty_version"
    // Logger
    compile "org.slf4j:slf4j-api:1.7.25"
    // Json
    compile "org.json:json:20190722"
    // 单元测试
    testCompile "junit:junit:4.12"
}


apply plugin: "de.fuerstenau.buildconfig"

static def s(Object o) { return o.toString() }

static def base64(File file) { return new String(Base64.encoder.encode(file.readBytes()), "UTF-8") }

buildConfig {
    appName = "LightTunnel"
    version = project.version
    clsName = "BuildConfig"
    packageName = "${rootProject.group}"
    charset = "UTF-8"

    buildConfigField "int", "VERSION_CODE", "${GitVC.VERSION_CODE}"
    buildConfigField "String", "VERSION_NAME", s(project.version)
    buildConfigField "String", "LAST_COMMIT_SHA", s(GitVC.LAST_COMMIT_SHA)
    buildConfigField "String", "LAST_COMMIT_DATE", s(GitVC.LAST_COMMIT_DATE)
    buildConfigField "String", "BUILD_DATA", s(BUILD_DATE)

    buildConfigField "String", "SERVER_JKS_BASE64", s(base64(rootProject.file("resources/lts.jks")))
    buildConfigField "String", "SERVER_JKS_STORE_PASSWORD", s("ltspass")
    buildConfigField "String", "SERVER_JKS_KEY_PASSWORD", s("ltspass")

    buildConfigField "String", "CLIENT_JKS_BASE64", s(base64(rootProject.file("resources/ltc.jks")))
    buildConfigField "String", "CLIENT_JKS_STORE_PASSWORD", s("ltcpass")

}

apply from: rootProject.file("./gradle/common.gradle").absolutePath
apply from: rootProject.file("./gradle/distjar.gradle").absolutePath
apply from: rootProject.file("./gradle/publish.gradle").absolutePath
